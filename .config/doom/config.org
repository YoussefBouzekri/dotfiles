
* Doom Emacs Configuration

#+begin_quote
Let us change our traditional attitude to the construction of programs: Instead of imagining that our main task is to instruct a computer what to do, let us concentrate rather on explaining to human beings what we want a computer to do. — Donald Knuth
#+end_quote
** TODO Introduction

** Rudimentary configuration
Make this file run slightly faster with lexical binding
#+begin_src elisp
;;; config.el -*- lexical-binding: t; -*-
#+end_src

*** Personal Information
It is useful to have some personal information, this is used for gpg, magit and such.
#+begin_src elisp
(setq user-full-name "spcbfr"
      user-mail-address "youssefbouzekri@protonmail.com")
#+end_src
*** Better defaults
#+begin_src elisp
(setq-default
 window-combination-resize t                      ; take new window space from all other windows (not just current)
 x-stretch-cursor t)                              ; Stretch cursor to the glyph width

(setq undo-limit 80000000                         ; Raise undo-limit to 80Mb
      evil-want-fine-undo t                       ; By default while in insert all changes are one big blob. Be more granular
      auto-save-default t                         ; Nobody likes to loose work, I certainly don't
      truncate-string-ellipsis "…"                ; Unicode ellispis are nicer than "...", and also save /precious/ space
      password-cache-expiry nil                   ; I can trust my computers ... can't I?
      ;; scroll-preserve-screen-position 'always     ; Don't have `point' jump around
      scroll-margin 2)                            ; It's nice to maintain a little margin

(unless (string-match-p "^Power N/A" (battery))   ; On laptops...
  (display-battery-mode 1))                       ; it's nice to know how much power you have

(global-subword-mode 1)                           ; Iterate through CamelCase words
#+end_src
** Doom configuration
*** Visual Settings
**** Font Face
Currently I am using Operator Mono as my main personal system font, it is pretty neat. doom allows you to customize the interface’s fonts through 5 different optional variables, for each variable you can set the family, size and weight.
#+begin_src elisp
(setq doom-font (font-spec :family "Jetbrains Mono" :size 13)
      doom-big-font (font-spec :family "JetBrains Mono" :size 24)
      doom-variable-pitch-font (font-spec :family "Overpass" :size 13)
      doom-unicode-font (font-spec :family "JuliaMono")
      doom-serif-font (font-spec :family "IBM Plex Mono" :weight 'light))
#+end_src
**** Theme and modeline
I find the default doom emacs ~doom-one~ theme to be pretty good, I even themed my whole system around it.
#+begin_src elisp
(setq doom-theme 'doom-vibrant)
(remove-hook 'window-setup-hook #'doom-init-theme-h)
(add-hook 'after-init-hook #'doom-init-theme-h 'append)
(delq! t custom-theme-load-path)
#+end_src

While we’re modifying the modeline, LF UTF-8 is the default file encoding, and thus not worth noting in the modeline. So, let’s conditionally hide it.
#+begin_src elisp
(defun doom-modeline-conditional-buffer-encoding ()
  "We expect the encoding to be LF UTF-8, so only show the modeline when this is not the case"
  (setq-local doom-modeline-buffer-encoding
              (unless (and (memq (plist-get (coding-system-plist buffer-file-coding-system) :category)
                                 '(coding-category-undecided coding-category-utf-8))
                           (not (memq (coding-system-eol-type buffer-file-coding-system) '(1 2))))
                t)))

(add-hook 'after-change-major-mode-hook #'doom-modeline-conditional-buffer-encoding)
#+end_src
**** Miscellaneous
Relative line numbers are fantastic for knowing how far away line numbers are, then ESC 12 <UP> gets you exactly where you think.
#+begin_src elisp
(setq display-line-numbers-type 'relative)
#+end_src

I’d like some slightly nicer default buffer names

#+begin_src elisp
(setq doom-fallback-buffer-name "► Doom"
      +doom-dashboard-name "► Doom")
#+end_src
*** Other Things
**** Window Title
Since I use a Window Manager only system, I don't usually see my window title but when I do, I like it to just have the buffer name, then if applicable the project folder
#+begin_src elisp
(setq frame-title-format
      '(""
        (:eval
         (if (s-contains-p org-roam-directory (or buffer-file-name ""))
             (replace-regexp-in-string
              ".*/[0-9]*-?" "☰ "
              (subst-char-in-string ?_ ?  buffer-file-name))
           "%b"))
        (:eval
         (let ((project-name (projectile-project-name)))
           (unless (string= "-" project-name)
             (format (if (buffer-modified-p)  " ◉ %s" "  ●  %s") project-name))))))
#+end_src
**** Splash Screen
emacs can render an image (~png~ or ~svg~ etc...), and I like to have the new emacs logo as my splash image
#+begin_src elisp
(setq fancy-splash-image "~/.config/doom/emacs-e.svg")
#+end_src
Lastly, the dashboard's set of useful commands are no longer useful to me (since I've basically memorized them all) so I prefer to keep clean look, I also like the disable the modeline when using dashboard to be even more minimal
#+begin_src elisp
(remove-hook '+doom-dashboard-functions #'doom-dashboard-widget-shortmenu)
(add-hook! '+doom-dashboard-mode-hook (hide-mode-line-mode 1) (hl-line-mode -1))
#+end_src
**** Dashboard Quick Actions
since the dashboard is it's own major mode, there is no need for the long key cords for the common commands, they can be replaced with single keystrokes
#+begin_src elisp


(map! :map +doom-dashboard-mode-map
      :ne "f" #'find-file
      :ne "r" #'consult-recent-file
      :ne "p" #'doom/open-private-config
      :ne "c" (cmd! (find-file (expand-file-name "config.org" doom-private-dir)))
      :ne "q" #'save-buffers-kill-terminal)
#+end_src
